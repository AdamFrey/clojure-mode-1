@external prop prefix from "./props"
@external prop coll from "./props"
@external prop startEdge from "./props"
@external prop endEdge from "./props"
@external prop prefixEdge from "./props"
@external prop sameEdge from "./props"

@top[name=Program] { expression* }

@skip { whitespace | LineComment | Discard }
expression { Boolean | Nil | Deref | Quote | SyntaxQuote | Unquote | UnquoteSplice | Symbol | Number | Keyword | List | Vector | Map | String | Character | Set | NamespacedMap | RegExp | Var | ReaderConditional | DataLiteral | SymbolicValue | AnonymousFunction | FnArg | Meta<expression> }
Discard { "#_" expression }
@precedence { docString @left, operator @left, meta @right }

listContents {
   defList { defLikeWithMeta varNameWithMeta (DocString expression+ | expression+)? } |
   anyList { operatorWithMeta? expression* }
 }

DocString { !docString String }
List[coll] { "(" listContents ")" }
Vector[coll] { "[" expression* "]" }
Map[coll] { "{" expression* "}" }
VarName { Symbol }

@skip {} {
  Set[prefix] { "#" Map }
  AnonymousFunction[prefix] { "#" List }
  NamespacedMap[prefix] { "#" KeywordPrefix Map }
  RegExp[prefix] { "#" String }
  Var[prefix] { "#'" Symbol }
  ReaderConditional[prefix] { "#?" (List | Deref) }
  DataLiteral[prefix] { "#" ident }
  SymbolicValue[prefix] { "##" simpleIdent }
  ReaderMetadata[prefix] { "#^" expression }
  Metadata[prefix] { "^" expression }
  String { '"' StringContent? '"' }
}

Meta<t> { (Metadata | ReaderMetadata) !meta t }

Deref[prefix] { "@" expression }
Quote[prefix] { "'" expression }
SyntaxQuote[prefix] { "`" expression }
Unquote[prefix] { "~" expression }
UnquoteSplice[prefix] { "~@" expression }
operatorWithMeta { Operator | Meta<operatorWithMeta> }
defLikeWithMeta { DefLike | Meta<defLikeWithMeta> }
varNameWithMeta { VarName | Meta<varNameWithMeta> }

Operator { !operator Symbol }

@tokens {


  "["[startEdge]
  "{"[startEdge]
  "("[startEdge]

  "#"[prefixEdge]
  "##"[prefixEdge]
  "#'"[prefixEdge]
  "#?"[prefixEdge]
  "#^"[prefixEdge]
  "#_"[prefixEdge]

  '"'[sameEdge, closedBy='"', openedBy='"']
  "'"[prefixEdge]
  "`"[prefixEdge]
  "~"[prefixEdge]
  "~@"[prefixEdge]
  "^"[prefixEdge]
  "@"[prefixEdge]


  "]"[endEdge]
  "}"[endEdge]
  ")"[endEdge]

  whitespace { (std.whitespace | ",")+ }

  LineComment { ";" ![\n]* }

  identStart { std.asciiLetter | $[<>._=?!*+\-$\u{a1}-\u{10ff}] }
  identChar { identStart | std.digit | ":" | "'" | "#" }
  simpleIdent { identStart identChar* }
  qualifiedIdent { simpleIdent "/" identChar+ }
  ident { simpleIdent | qualifiedIdent }
  Symbol { ident }

  keyword { ":" ":"? ident }
  Keyword { keyword }
  KeywordPrefix[prefixEdge] { keyword }

  Number {
    ("+" | "-")? (std.digit+ ("." std.digit* "M"?)? | "." std.digit+) (("e" | "E") ("+" | "-")? std.digit+ "M"?)? |
    ("+" | "-")? std.digit+ ("M" | "N") |
    ("+" | "-")? std.digit+ "/" std.digit+ |
    ("+" | "-")? "0x" (std.digit | $[a-fA-F])+ |
    "0b" $[01]+ |
    "0o" $[0-7]+
  }
  @precedence { Number, Symbol }

  StringContent {
    (!["] | "\\" _)+
  }

  Character { "\\" (std.asciiLetter | std.digit | "@")+ }

  FnArg[name=Symbol] { "%" (std.digit* | "&"?) }

}

Boolean { @specialize<Symbol, "true" | "false"> }
Nil { @specialize<Symbol, "nil"> }
DefLike[dynamicPrecedence=1] { @extend<Symbol, "def" | "defn" | "defn-" | "defmacro" | "definline" | "defonce" | "deftest" | "defcard"> }

@detectDelim
